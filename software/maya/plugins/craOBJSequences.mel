/*
'OBJ Sequence Import/Export' MEL script

Author: Carlos Rico Adega - carlos.rico.3d@gmail.com
Date: May 3, 2013

Copyright (c) 2015 Carlos Rico Adega

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:                         

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,k
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.

SCRIPT NAME:

craOBJSequences.mel

Main procedure:
craOBJSequences;


DESCRIPTION:

- This script helps to import and export OBJ sequences.
- It uses choice nodes to handle the animation of imported sequences.
- Allows to export seleted objects as one sequence (combining objects)
or one sequence per object.

LAUNCH SCRIPT :

craOBJSequences;


Version: 1.2.1 TESTED ON:

Windows 7 x64
- Maya 2013 x64

Fedora 14 x64 ( Virtualbox Virtual Machine )
- Maya 2012 x64


CHANGE LOG:

Version: 2.0.2
Date: December 1, 2015
        - Maya 2016+ support

Version: 2.0.1
Date: March 10, 2015
        - Fixed a bug that caused the script to stop working when importing sequences
          without materials (trying to rename lambert1 read-only node)
        
Version: 2.0.0
Date: March 5, 2015
        - Added attributes to imported sequences (animated, cycle animation and offset initial frame)
        - The tool now supports to assign materials when importing
          (materials with textures only seem to work in Maya 2014+)
        - The animation is now controlled by an expression
        - UI redesign
        
Version: 1.2.1
Date: May 27, 2014
        - Fixed minor Maya 2015 UI glitches
        
Version: 1.2
Date: November 22, 2013
        - Added export options
        
Version: 1.1.1
Date: October 10, 2013
        - Added flushUndo command for clearing RAM (thanks to Lossless22 for the tip)

Version: 1.1.0
Date: May 8, 2013
        - Import/Export are now interruptable processes.
        - Fixed a bug that caused the script to stop working when a file fails to import.

Version: 1.0.0
Date: May 3, 2013
        - First Release.

*/

global proc craOBJSequences()
{
    if(!`pluginInfo -q -l "objExport"`)
        loadPlugin -quiet "objExport";
    
    if (`window -exists craOBJSequencesWin`)
        deleteUI craOBJSequencesWin;
    
    if(!`optionVar -exists craOBJCollapseExportOptionsOV`)
		optionVar -iv craOBJCollapseExportOptionsOV 1;
	
	if(!`optionVar -exists craOBJGroupsOV`)
		optionVar -iv craOBJGroupsOV 1;
	
	if(!`optionVar -exists craOBJPointGroupsOV`)
		optionVar -iv craOBJPointGroupsOV 1;
	
	if(!`optionVar -exists craOBJMaterialsOV`)
		optionVar -iv craOBJMaterialsOV 1;
	
	if(!`optionVar -exists craOBJSmoothingOV`)
	    optionVar -iv craOBJSmoothingOV 1;
	
	if(!`optionVar -exists craOBJNormalsOV`)
		optionVar -iv craOBJNormalsOV 1;
	
	if(!`optionVar -exists craOBJsApplyMatsOV`)
		optionVar -iv craOBJsApplyMatsOV 1;
	
	window -t "  OBJ Sequences I/O" -tlb 1 -s 0 craOBJSequencesWin;
        frameLayout -mw 2 -mh 2 -lv 0 craImportExportOBJsMainFL;
                columnLayout -adj 1 craImportExportOBJsMainCL;
                        frameLayout -mw 2 -mh 2 -lv 0 -l "Import Sequence" -cll 0 craImportExportOBJsFL1;
                                columnLayout -adj 1 craImportExportOBJsCL1;
									rowLayout -nc 4 -adj 2;
										text -w 3 -l "";
										checkBox -v 1 -l " One transform" -ann "If checked all imported meshes will be\ncollapsed under one transform. If\nunchecked, groups will be created \nto hold the imported meshes (allows\nto edit individual frames)." craOBJSeqOutputCB;

										iconTextCheckBox -v `optionVar -q craOBJsApplyMatsOV` 
											-cc "optionVar -iv craOBJsApplyMatsOV #1"
											-onc "iconTextCheckBox -e -i \"Textured.png\" -ann \"Assign materials on import ENABLED\" craOBJsApplyMatsITCB"
											-ofc "iconTextCheckBox -e -i \"out_lambert.png\" -ann \"Assign materials on import DISABLED\" craOBJsApplyMatsITCB"
											-w 24 -h 24 -ann "Toggle assign materials on import"
											-i "Textured.png" 
											craOBJsApplyMatsITCB;
											text -w 2 -l "";

									setParent ..;
									if(`optionVar -q craOBJsApplyMatsOV`)
										iconTextCheckBox -e -i "Textured.png" -ann "Assign materials on import ENABLED" craOBJsApplyMatsITCB;
									else
										iconTextCheckBox -e -i "out_lambert.png" -ann "Assign materials on import DISABLED" craOBJsApplyMatsITCB;

									separator -h 2 -st "none";
									nodeIconButton -style "textOnly" -h 22 -bgc .5 .7 .5 -l "Import OBJ Sequence" -c ("craImportOBJs") -ann "Select OBJ Sequence files to import" craOBJSequenceImportNIB;
                                setParent ..;
                        setParent ..;
                separator -h 2 -st "none";
                
                frameLayout -mw 2 -mh 2 -lv 0 -l "Export Sequence" -cll 0 craImportExportOBJsFL2;
                        columnLayout -adj 1 craImportExportOBJsCL2;
                                rowLayout -nc 4 -adj 4;
                                        nodeIconButton -w 33 -style "textOnly" -h 20 -c "intField -e -v `currentTime -q` craOBJsStartFrameIF" -l "start" -ann "Click to set start frame to current frame";
                                        intField -w 32 -ann "Start frame" -v `playbackOptions -q -min` craOBJsStartFrameIF;
                                        nodeIconButton -w 26 -style "textOnly" -h 20 -l "end" -c "intField -e -v `currentTime -q` craOBJsEndFrameIF" -ann "Click to set end frame to current frame";
                                        intField -w 25 -ann "End frame" -v `playbackOptions -q -max` craOBJsEndFrameIF;
                                setParent ..;
                                
                                separator -h 3 -st "none";
                                frameLayout -cll 1 -mw 12 -mh 4
                                        -ec "optionVar -iv craOBJCollapseExportOptionsOV 0;window -e -h 277 craOBJSequencesWin" 
                                        -cc "optionVar -iv craOBJCollapseExportOptionsOV 1;window -e -h 180 craOBJSequencesWin" 
                                        -cl `optionVar -q craOBJCollapseExportOptionsOV` 
                                        -l "Export Options";
                                        
                                        columnLayout -adj 1;
											checkBox -w 75 -l "  Groups" -v `optionVar -q craOBJGroupsOV`
													-cc "optionVar -iv craOBJGroupsOV #1"; 
											checkBox -l "  Point Groups" -v `optionVar -q craOBJPointGroupsOV` 
													-cc "optionVar -iv craOBJPointGroupsOV #1";
											checkBox -w 75 -l "  Materials" -v `optionVar -q craOBJMaterialsOV` 
													-cc "optionVar -iv craOBJMaterialsOV #1";
											checkBox -l "  Smoothing" -v `optionVar -q craOBJSmoothingOV` 
													-cc "optionVar -iv craOBJSmoothingOV #1";
											checkBox -w 75 -l "  Normals" -v `optionVar -q craOBJNormalsOV` 
													-cc "optionVar -iv craOBJNormalsOV #1";
                                        setParent ..;
                                setParent ..;
                                
                                separator -h 3 -st "none";
                                    nodeIconButton -style "textOnly" -h 22 -bgc .7 .5 .2 -l "Export OBJ Sequence" -c "craExportOBJs";
                                setParent ..; 
                        setParent ..;
                        separator -h 2 -st "none";
                        frameLayout -mw 2 -mh 2 -lv 0 -l "Sequences Toolbar" -cll 0;
							rowLayout -nc 6 -adj 5;
								nodeIconButton -w 24 -h 24 -ann "Toggle cycle animation on selected" -style "iconOnly" -i "postInfinityCycle.png" -c "if(size(`ls -sl`))\n{\n    for($seq in `ls -sl`)\n        setAttr ($seq + \".cycleAnimation\") (`getAttr ($seq + \".cycleAnimation\")` ? 0 : 1);\n}";;
								nodeIconButton -w 24 -h 24 -ann "Assign default material" -style "iconOnly" -i "out_lambert.png" -c "hyperShade -assign lambert1";;
								nodeIconButton -w 24 -h 24 -ann "Delete Unused Materials" -style "iconOnly" -i "deleteActive.png" -c "hyperShadePanelMenuCommand(\"hyperShadePanel1\", \"deleteUnusedNodes\");";;
								nodeIconButton -w 24 -h 24 -ann "Delete OBJ Sequences" -style "iconOnly" -i "SP_TrashIcon.png" -c "if(size(`ls -o \"*.isOBJSequence\"`)) delete `ls -o \"*.isOBJSequence\"`;";;
								text -l "" -ann "Carlos Rico Adega © 2015";
								nodeIconButton -ann "About this Tool" -c "craAboutOBJSequences" -i "pickOtherObj.png" -w 24 -h 24;
							setParent ..;
						setParent ..;
                setParent ..;
        setParent ..;
        
        if(!`optionVar -q craOBJCollapseExportOptionsOV`)
			window -e -wh 151 277 -s 0 craOBJSequencesWin;
		else
			window -e -wh 151 180 -s 0 craOBJSequencesWin;

        showWindow craOBJSequencesWin;

        setFocus craOBJSequenceImportNIB;
}

global proc craExportOBJs( )
{
        string $mySel[] = `ls -sl`;
        if(!size($mySel))
				error "Nothing is selected. Select object(s) to export.";
        
		int $multiple = 0;

        int $craCancelled;

        string $craName;
        string $craResult = `promptDialog
        -title "OBJ Sequence"
        -message "Enter sequence name:"
        -button "OK" -button "Cancel"
        -defaultButton "OK" -cancelButton "Cancel"
        -dismissString "Cancel"`
        
        ;
        if ($craResult == "OK")
        {
			string $craOBJExportOptions;
			$craOBJExportOptions += "groups=" + string(`optionVar -q craOBJGroupsOV`) + ";";
			$craOBJExportOptions += "ptgroups=" + string(`optionVar -q craOBJPointGroupsOV`) + ";";
			$craOBJExportOptions += "materials=" + string(`optionVar -q craOBJMaterialsOV`) + ";";
			$craOBJExportOptions += "smoothing=" + string(`optionVar -q craOBJSmoothingOV`) + ";";
			$craOBJExportOptions += "normals=" + string(`optionVar -q craOBJNormalsOV`);
			//"groups=1;ptgroups=1;materials=0;smoothing=1;normals=1";
			if(size($mySel) > 1)
			{
				string $answer = `confirmDialog -title "Question" -message "                     Multiple objects selected. What do you want to do?"
					-button " One OBJ sequence for all objects " -button " One OBJ sequence per object " -button "Cancel" -defaultButton "One OBJ sequence for all objects"
					-cancelButton "Cancel" -dismissString "Cancel"`;
				if($answer == " One OBJ sequence per object ")
					$multiple = 1;
				
				else if($answer == "Cancel")
					return;
			}
			
			$craName = `promptDialog -query -text`;
			
			string $exportDir[] = `fileDialog2 -caption "Select Export Folder" -okCaption "Export" -fileMode 3 -dialogStyle 2`;
			
			$startTime = `timerX`;
			
			if(size($exportDir))
			{
					int $start = `intField -q -v craOBJsStartFrameIF`;
					int $end = `intField -q -v craOBJsEndFrameIF`;
					
					int $maxValue = $end - $start;
					progressWindow  -status "Exporting OBJ Sequence..." -maxValue $maxValue -title "Exporting" -isInterruptable true;
					
					currentTime -e $start;
					dgeval $mySel[0];
					
					for( $crai=$start; $crai<=$end; $crai++ )
					{
						if ( `progressWindow -query -isCancelled` )
						{
							$craCancelled = 1;
							break;
						}
						
						progressWindow -e -step 1 ;
						
						if($multiple)
						{
							for($j in $mySel)
							{
								select -r $j;
								dgeval $j;
								duplicate;
								craOBJsChannelControlProc {"tx", "ty","tz","rx","ry","rz","sx","sy","sz","v"} 0 1;
								catchQuiet(`parent -w`);
								sysFile -md ($exportDir[0] + "/" + $craName + "_OBJ_Seq/" + $j);
								file -force -options $craOBJExportOptions -typ "OBJexport" -pr -es ($exportDir[0] + "/" + $craName + "_OBJ_Seq/" + $j + "/" + $j + "." + craNumberPadding (string($crai), 4, "0") + ".obj");
								delete;
							}
						}
						else
						{
							select -r $mySel;
							duplicate;
							if(size($mySel) > 1)
								polyUnite -ch 0;
							else
								catchQuiet(`parent -w`);
							craOBJsChannelControlProc {"tx", "ty","tz","rx","ry","rz","sx","sy","sz","v"} 0 1;
							sysFile -md ($exportDir[0] + "/" + $craName + "_OBJ_Seq");
							file -force -options $craOBJExportOptions -typ "OBJexport" -pr -es ($exportDir[0] + "/" + $craName + "_OBJ_Seq/" + $craName + "." + craNumberPadding (string($crai), 4, "0") + ".obj");
							delete;
						}
						flushUndo;
						currentTime -e ($crai+1);
					}
			}
			
			select -r $mySel;
			
			progressWindow -endProgress;
			
			$totalTime = `timerX -startTime $startTime`;
			
			if($craCancelled)
				warning "Exporting OBJ sequence cancelled.\n";
			else
				print ("All done! " + $craName + " exported successfully in " + $totalTime + " seconds.\n");
        }
}

global proc craImportOBJs()
{
        int $mode = `checkBox -q -v craOBJSeqOutputCB`;
        
        string $OBJfiles[] = `fileDialog2
            -caption "Select OBJ Sequence files to import"
            -okCaption "Import"
            -fileMode 4
            -fileFilter "Wavefront Object (*.obj) (*.obj)"
            -sff "Wavefront Object (*.obj) (*.obj)"
            -dialogStyle 2`;
        
        if(size($OBJfiles))
                craImportOBJsCmd($OBJfiles, $mode);
        else
                print "No files selected.\n";
}

global proc craImportOBJsCmd( string $OBJfiles[], int $mode )
{
        $startTime = `timerX`;
        string $craErrors[];
        int $craCancelled;
        
        // store the status of "removeDuplicateShadingNetworksOnImport" optionVar
        int $rdsnoiStatus = `optionVar -q "removeDuplicateShadingNetworksOnImport"`;
        // set to remove duplicated shading networks
        optionVar -iv "removeDuplicateShadingNetworksOnImport" 1;
        
        string $origMeshNodes[] = `ls -l -type "mesh"`;
        string $craOrigObjectSetNodes[] = `ls -type "objectSet"`;
        string $currentMeshNodes[] = `ls -l -type "mesh"`;
        
        if(!$mode)
        {
            if(!`objExists "OBJ_Sequences_grp"`)
            {
				group -em -n "OBJ_Sequences_grp";
				addAttr -ln ("isOBJSequence") -at bool -dv 0 OBJ_Sequences_grp;
				setAttr -e -lock true "OBJ_Sequences_grp.isOBJSequence" on;
            }
		}
        
        string $filename = basenameEx("\"" + $OBJfiles[0] + "\"");
        string $groupName[];
        string $OBJshaders[];
        string $OBJshaders_assignments[];
        tokenize $filename "." $groupName;
        tokenize $groupName[0] "_" $groupName;
        
        select -cl;
        int $status = `scriptEditorInfo -q -sr`;
        scriptEditorInfo -sr 1;
        int $count = 1;
        int $maxValue = size($OBJfiles);
        
        progressWindow  -status "Importing OBJ Sequence..." -maxValue $maxValue -title "Importing" -isInterruptable true;
        
        for($file = 0; $file < size($OBJfiles); $file++)
        {
                if ( `progressWindow -query -isCancelled` )
                {
					$craCancelled = 1;
					break;
                }
                
                if(catchQuiet(`file -force -import -rdn -type "OBJ" -options "mo=0" $OBJfiles[$file]`))
                {
					$craErrors[size($craErrors)] = $OBJfiles[$file];
					continue;
                }
                
                select -r `ls -l -type "mesh"`;
                select -deselect $currentMeshNodes;
                
                if($file == 0)
					$OBJshaders = craOBJSequencesGetShaders();
                
                if(!$mode)
                        pickWalkUp;
                    
                string $newMesh[] = `ls -sl`;
                
                if(size($newMesh))
                        rename $newMesh[0] ( $groupName[0] + "_" + craNumberPadding(string($count), 4, 0) + "_mesh" );
                else
                        $craErrors[size($craErrors)] = $OBJfiles[$file];
                
				if(!$mode)
                {
					select -add "OBJ_Sequences_grp";
					parent;
                }
                
                hide;
                
                $currentMeshNodes = `ls -l -type "mesh"`;
                $count++;
                flushUndo;
                progressWindow -e -step 1 ;
        }
        
        select -r `ls -l -type "mesh"`;
        select -deselect $origMeshNodes;
        
        
        if(!$mode)
        {
			pickWalkUp;
			$newGroup = `group -n ($groupName[0] + "_OBJ_seq")`;
			select -r `listRelatives -f -c $newGroup`;
        }
        
        craConnectToChoice $groupName[0] $mode;
        
        // remove imported sets
        string $craCurrentSets[] = `ls -type "objectSet"`;
        string $craRemoveSets[] = `stringArrayRemove $craOrigObjectSetNodes $craCurrentSets`;
        delete $craRemoveSets;
        
		if(size($OBJshaders) > 1)
		{

			string $currentSel[] = `ls -sl`;
			for($shd=0; $shd < size($OBJshaders); $shd+=2)
			{
			    if($OBJshaders[$shd] == "lambert1")
			        continue;
				select -r $OBJshaders[$shd+1];
				string $shaderNamed = `rename $OBJshaders[$shd] ($groupName[0] + "_OBJ_Seq_mat")`;
				if(`iconTextCheckBox -q -v craOBJsApplyMatsITCB`)
					hyperShade -assign $shaderNamed;
				select -r $currentSel[0];
			}
		}
		else
		{
		    if($OBJshaders[0] != "lambert1")
		    {
                string $shaderNamed = `rename $OBJshaders[0] ($groupName[0] + "_OBJ_Seq_mat")`;
                if(`iconTextCheckBox -q -v craOBJsApplyMatsITCB`)
                    hyperShade -assign $shaderNamed;
			}
		}
        
        scriptEditorInfo -sr $status;

        $totalTime = `timerX -startTime $startTime`;
        
        if(size($craErrors))
        {
			for($crai in $craErrors)
				print ($crai + " failed to import.\n");
                
			warning ("There were errors importing " + size($craErrors) + " files. See Script Editor for details.\n");
        }
        else
        {
			if($craCancelled)
				warning "Importing OBJ sequence cancelled.\n";
			else
				print ("All done! " + $groupName[0] + " sequence imported successfully in " + $totalTime + " seconds.\n");
        }
        progressWindow -endProgress;
        
        // restore "removeDuplicateShadingNetworksOnImport" optionVar status
        optionVar -iv "removeDuplicateShadingNetworksOnImport" $rdsnoiStatus;
}

//craNumberPadding ("20", 4, "0")
global proc string craNumberPadding(string $str,int $pad,string $chr)
{
        string $craReturn;
        
        for ($crai=0;$crai<$pad;$crai++)
			if (size($str)<=$crai)
				$craReturn += $chr;
        
        return ($craReturn + $str);
}


// Usage:
//    Select your objects and run. It creates a new choice node, and a new target mesh.
//    Connects every shape node to the choice node. No error checking so select only meshes.

global proc craConnectToChoice( string $craName , int $mode)
{
        // get selection
        string $mySel[] = `ls -sl`;
        
        // create choice node with name
        string $choice = `createNode -n ($craName + "_OBJ_Seq_choice") choice`;
        
        // create new mesh to hold the animation
        string $newMesh[] = `polyCube -ch 0 -n ($craName + "_OBJ_Seq_Mesh")`;
        
        // connect choice output to new mesh inMesh attr
        string $meshShape[] = `listRelatives -s $newMesh[0]`;
        connectAttr -f ($choice + ".output") ($meshShape[0] + ".inMesh");
        
        // create attributtes to control the animation
        addAttr -ln ("OBJ_SEQUENCE_OPTIONS") -at char -dv 0 $newMesh[0];
        setAttr -e -keyable true -lock true ($newMesh[0] + ".OBJ_SEQUENCE_OPTIONS");
        
        addAttr -ln ("animated") -at "enum" -en "off:on:" -dv 0 $newMesh[0];
        setAttr -e -keyable true ($newMesh[0] + ".animated") on;
        
        addAttr -ln ("cycleAnimation") -at "enum" -en "on:off:" -dv 0 $newMesh[0];
        setAttr -e -keyable true ($newMesh[0] + ".cycleAnimation") 0;
        
        addAttr -ln ("offsetInitialFrame") -at long -dv 0 $newMesh[0];
        setAttr -e -keyable true ($newMesh[0] + ".offsetInitialFrame");
        
        addAttr -ln ("isOBJSequence") -at bool -dv 0 $newMesh[0];
        setAttr -e -lock true ($newMesh[0] + ".isOBJSequence") on;

        expression -s ("selector = fmod((frame * " + $newMesh[0] + ".animated) + " + $newMesh[0] + ".offsetInitialFrame*-1, " + size($mySel) + " + (frame * " + $newMesh[0] + ".cycleAnimation))") -o $choice -ae 1 -uc all -n ($choice + "_selector_expr");

        // connect obj outMesh to choice input attr
        for( $crai=0; $crai<size($mySel) ;$crai++ )
        {
			if(!$mode)
			{
				string $shapes[] = `listRelatives -s -f $mySel[$crai]`;
				connectAttr -f ($shapes[0] + ".outMesh") ($choice + ".input[" + ($crai) + "]");
			}
			else
			{
				connectAttr -f ($mySel[$crai] + ".outMesh") ($choice + ".input[" + ($crai) + "]");
				setAttr ($mySel[$crai] + ".intermediateObject") 1;
				setAttr ($mySel[$crai] + ".visibility") 1;
				parent -s -add $mySel[$crai] $newMesh[0];
			}
        }
        
        if($mode)
			delete `listRelatives -f -p $mySel`;
        else 
        {
            select -r `listRelatives -f -p $mySel`;        
            sets -e -forceElement initialShadingGroup $mySel;
        }
        
        select -r $newMesh[0];
}

global proc craAboutOBJSequences()
{
        if (`window -exists craAboutOBJSequencesWin`)
			deleteUI craAboutOBJSequencesWin;
        
        window -t  " About / Help" -s 0 -tlb 1 craAboutOBJSequencesWin;
        frameLayout -mw 5 -mh 5 -lv 0;
        frameLayout -mw 5 -mh 5 -lv 0;
        columnLayout -adj 1;
        text -l "<font color=gold><h3>OBJ Sequences Import/Export</h3></font>";
        text -l "<font color=indianred><h3>v2.0.2</h3></font>";
        separator -h 5 -st "none";
        frameLayout -mw 5 -mh 5 -lv 0;
            text -l "<font color=LimeGreen><h2>HELP</h2></font>";
            text -l "<font color=#057cf3><h4>One transform</h4></font>";
            text -l "If checked all imported meshes will be\ncollapsed under one transform. If\nunchecked, groups will be created\nto hold the imported meshes (allows\nto edit individual frames)" -al "center";
            rowLayout -nc 3 ;
				text -l "  ";
				iconTextCheckBox -v `optionVar -q craOBJsApplyMatsOV` -w 24 -h 24 -i "Textured.png" -ann "Assign materials on import toggle";
				text -l "   Toggle assign materials\non import";
            setParent ..;
            text -l "<font color=#057cf3><h4>Import Sequence</h4></font>";
            text -l "Shows a file dialog for selecting\nthe OBJ sequence files to import\n" -al "center";
            text -l "<font color=#057cf3><h4>Export Sequence</h4></font>";
            text -l "If multiple objects are selected a dialog\nshows up to choose whether to make\none OBJ sequence for all objects or\none OBJ sequence for each object\n" -al "center";
            rowLayout -nc 3 ;
				text -l "";
				nodeIconButton -w 24 -h 24 -ann "Toggle cycle animation on selected" -style "iconOnly" -i "postInfinityCycle.png";
				text -l " Toggle cycle animation on selected";
            setParent ..;
            rowLayout -nc 3 ;
				text -l "" ;
				nodeIconButton -w 24 -h 24 -ann "Assign default material" -style "iconOnly" -i "out_lambert.png";
				text -l " Assign default material to selected";
            setParent ..;
            rowLayout -nc 3 ;
				text -l "";
				nodeIconButton -w 24 -h 24 -ann "Delete Unused Materials" -style "iconOnly" -i "deleteActive.png";
				text -l " Delete Unused Materials";
            setParent ..;
            rowLayout -nc 3 ;
				text -l "";
				nodeIconButton -w 24 -h 24 -ann "Delete OBJ Sequences" -style "iconOnly" -i "SP_TrashIcon.png" -c "";;
				text -l " Delete OBJ Sequences";
            setParent ..;
            text -l "" -h 1;
        setParent ..;
        separator -h 5 -st "none";
        text -l "<font color=indianred><h4>Carlos Rico Adega © 2015</h4></font>";
        separator -h 5 -st "none";
        textField -ed 0 -text "              carlos.rico.3d@gmail.com";
        separator -h 5 -st "none";
        rowLayout -nc 2 -adj 1;
                button -w 70 -l "Downloads" -ann "Free downloads from CreativeCrash.com" -c "showHelp -absolute \"http://www.creativecrash.com/users/charliewales/contributions?tab=free_downloads\"";
                button -w 105 -ann "Linked[In] profile"-l "Linked[In]" -c "showHelp -absolute \"http://www.linkedin.com/pub/carlos-rico/6/58/325\"";
        setParent ..;
        rowLayout -nc 2 -adj 1;
                button -l "Vimeo" -ann "Vimeo Channel" -c "showHelp -absolute \"https://vimeo.com/channels/749131\"";
                button -w 105 -l "YouTube" -ann "Youtube Channel" -c "showHelp -absolute \"https://www.youtube.com/channel/UCXnSX8PHpQtwJR-uN-atO3Q\"";
        setParent ..;
        setParent ..;
        
        window -e -wh 239 600 -s 0 craAboutOBJSequencesWin;
        showWindow craAboutOBJSequencesWin;
}

global proc craOBJsChannelControlProc( string $attrs[], int $lock , int $keyable)
{
        string $listedObjects[] = `ls -sl -l`;
        
        for($attr in $attrs)
			for ($object in $listedObjects)
				if(`attributeExists $attr $object`)
					catchQuiet(`setAttr -l $lock -k $keyable ($object + "." + $attr)`);
}


global proc string[] craOBJSequencesGetShaders() 
{
    string $origSel[] = `ls -sl`;
    
    string $shaders[];
    string $shaders_assign[];
    // find the shading engine
    string $SG[] = `listConnections -source false -type "shadingEngine"`;
    //find the maya software shader
    for($crai in $SG)
    {
        string $currentShader[] = `listConnections -s 1 ($crai + ".surfaceShader")`;
        //find the mental ray shader
        if(!size($currentShader))
            $currentShader = `listConnections -s 1 ($crai + ".miMaterialShader")`;

        $shaders[size($shaders)] = $currentShader[0];
    }
    //add shader
    if(size($shaders) > 1)
    {
        for($cras in $shaders)
        {
            $shaders_assign[size($shaders_assign)] = $cras;
            hyperShade -objects $cras;

            string $mySel[] = `ls -sl`;
            string $buffer[];
            tokenize $mySel[0] "." $buffer;
            $shaders_assign[size($shaders_assign)] = "." + $buffer[size($buffer)-1];
        }
        select -r $origSel;
        return $shaders_assign;
    }
    else
    {
        select -r $origSel;
        return $shaders;
    }
}